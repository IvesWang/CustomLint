apply plugin: 'com.android.library'

ext {
    bintrayRepo = 'maven'
    bintrayName = 'custom-lint'

    publishedGroupId = 'com.github.iveswang'
    libraryName = 'custom-lint'
    artifact = 'custom-lint'

    libraryDescription = 'a custom lint tool for IntelliJ IDEA'

    siteUrl = 'https://github.com/IvesWang/CustomLint'
    gitUrl = 'https://github.com/IvesWang/CustomLint.git'

    libraryVersion = '1.0'

    developerId = 'ives'
    developerName = 'Ives Wang'
    developerEmail = 'youji3hao@126.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}
// 定义方法lintJarImport
configurations {
    lintJarImport
}

// 链接到lintJar中的lintJarOutput方法,调用jar方法，并获得jar包
dependencies {
//    lintJarImport project(path: ':lint-rules', configuration: 'lintJarOutput')//从java module:lint-rules中引入jar包
    lintJarImport fileTree(dir: 'libs', include: ['*.jar'])//从libs文件夹下引入jar包
}

// 将得到的JAR包复制到目录build/intermediates/lint/下
task copyLintJar(type: Copy) {
    from (configurations.lintJarImport) {
        rename {
            String fileName ->
                'lint.jar'
        }
    }
    into 'build/intermediates/lint/'
}
// 当项目build到compileLint这一步时执行copyLintJar方法
project.afterEvaluate {
    def compileLintTask = project.tasks.find{ it.name == 'compileLint'}
    compileLintTask.dependsOn(copyLintJar)
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
}

apply from: "/build_install.gradle"
apply from: "/build_bintray.gradle"
